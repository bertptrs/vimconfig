# vim: setf zsh

# Allow color escape sequences to be available
autoload -U colors && colors

# Allow calling functions inside prompt
setopt PROMPT_SUBST

PROMPT_VENV_INDICATOR="üêç"

if [[ $(echo $PROMPT_VENV_INDICATOR | wc -L) -ne 2 ]]; then
	# Workaround for older systems
	PROMPT_VENV_INDICATOR="p:"
fi

function prompt_last_cmd() {
	if [[ $HISTCMD -gt 0 ]]; then
		echo $history[$((HISTCMD - 1))]
	fi
}

function prompt_handle_virtualenv() {
	if [[ -n $VIRTUAL_ENV ]]; then
		echo "%{$fg_bold[green]%}%S$PROMPT_VENV_INDICATOR%s $(basename $VIRTUAL_ENV)%b%f%k "
	fi
}

function prompt_ps1() {
	local ps1 rs='%b%f%k%s'
    local dir="%{$fg[%~]%}%8~$rs"
	local hist="%{$fg[blue]%}%(?..%S)[\$(prompt_last_cmd)]$rs "
	local host="%{$fg[red]%}%m$rs "
	# Highlight the hostname if we are connected over SSH
	if [[ -n $SSH_TTY ]]; then
		host="%S%B$host"
	fi

	local prompt="%n@%{$fg[cyan]%}%m$rs %# "
	ps1=(
		"$host"
		"$hist"
		"$dir"
		$'\n'
		'$(prompt_handle_virtualenv)'
		"$prompt"
	)
    echo "${(j::)ps1}"
}

function prompt_rprompt() {
	local rprompt rs='%b%f%k' hist1 hist2
	local exit_status="%(?..%{$fg[red]%}- %{$fg_bold[yellow]%}%? %b%{$fg[red]%}-$rs )"
	local datetime="%D %{$fg[red]%}%T%{$reset_color%}"
	rprompt=(
		"$exit_status"
		"$datetime"
	)
    echo "${(j::)rprompt}"
}

PS1="$(prompt_ps1)"
RPROMPT="$(prompt_rprompt)"

# We handle virtualenv ourselves, no need to do it twice.
export VIRTUAL_ENV_DISABLE_PROMPT=1
